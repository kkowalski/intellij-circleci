buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'java'
    id 'idea'
    id 'org.jetbrains.intellij' version '0.6.5'
    id "org.jetbrains.kotlin.jvm" version "1.4.21"
}

def circleCIBuildNum = System.getenv('CIRCLE_BUILD_NUM')
def versionWithBuildNum = pluginVersion + "." + circleCIBuildNum
def channel = System.getenv('HUB_CHANNEL') ? System.getenv('HUB_CHANNEL') : ''

group pluginGroup
version = circleCIBuildNum ? versionWithBuildNum : pluginVersion

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
//  options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

test {
    testLogging {
        events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
        exceptionFormat = 'full'
        showStandardStreams = true
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    compile group: 'com.fasterxml.jackson.module', name: 'jackson-module-kotlin', version: '2.10.1'
}

intellij {
    pluginName = "intellij-circleci"

    version ideaVersion
    println "Building for IntelliJ version: ${version}"

    updateSinceUntilBuild = false
}

patchPluginXml {
    changeNotes """
      Fixes:
      <ul>
        <li>Settings are now per project</li>
        <li>Action messages</li>
        <li>Build status is automatically updated on check</li>
      </ul>"""

    sinceBuild 203.5981 // default
}

buildPlugin {
    archiveBaseName.set("intellij-circleci")
}

publishPlugin {
    token System.getenv('HUB_PUBLISH_TOKEN')
    channels channel
}
